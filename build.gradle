apply plugin: 'groovy'
apply plugin: 'maven'

repositories {
    jcenter()
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:2.4.6"

    compileOnly "org.apache.camel:camel-test:2.16.2"
    compileOnly "org.mockito:mockito-all:1.10.19"
}

group = "com.gmaslowski"
archivesBaseName = "camel-test-support"
version = "0.0.1"

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Apache Camel Test Support'
                packaging 'jar'

                description 'Apache Camel Test Support.'
                url 'http://github.com/gmaslowski/camel-test-support'

                scm {
                    connection 'scm:git:git@github.com:gmaslowski/camel-test-support.git'
                    developerConnection 'scm:git:git@github.com:gmaslowski/camel-test-support.git'
                    url 'http://github.com/gmaslowski/camel-test-support'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'gmaslowski'
                        name 'Greg Maslowski'
                        email 'gregmaslowski@gmail.com'
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}